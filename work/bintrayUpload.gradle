apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

// load properties
Properties properties = new Properties()
File localPropertiesFile = project.file("local.properties")
if(localPropertiesFile.exists()){
    properties.load(localPropertiesFile.newDataInputStream())
}
File projectPropertiesFile = project.file("project.properties")
if(projectPropertiesFile.exists()){
    properties.load(projectPropertiesFile.newDataInputStream())
}

// read properties
def projectName = properties.getProperty("project.name")
def projectGroupId = properties.getProperty("project.groupId")
def projectArtifactId = properties.getProperty("project.artifactId")
def projectVersionName = android.defaultConfig.versionName
def projectPackaging = properties.getProperty("project.packaging")
def projectSiteUrl = properties.getProperty("project.siteUrl")
def projectGitUrl = properties.getProperty("project.gitUrl")

def developerId = properties.getProperty("developer.id")
def developerName = properties.getProperty("developer.name")
def developerEmail = properties.getProperty("developer.email")

def bintrayUser = properties.getProperty("bintray.user")
def bintrayApikey = properties.getProperty("bintray.apikey")

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                artifact releaseSourcesJar

                // You can then customize attributes of the publication as shown below.
                groupId = projectGroupId
                artifactId = projectArtifactId
                version = projectVersionName

                pom {
                    name = projectName
                    packaging = projectPackaging
                    url = projectSiteUrl
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = developerId
                            name = developerName
                            email = developerEmail
                        }
                    }
                    scm {
                        connection = projectGitUrl
                        developerConnection = projectGitUrl
                        url = projectSiteUrl
                    }
                }
            }
        }
    }

    // bintray configuration
    bintray {
        user = bintrayUser
        key = bintrayApikey
        publications = ['release']
        pkg {
            repo = "maven"
            name = "work"
            websiteUrl = projectSiteUrl
            vcsUrl = projectGitUrl
            licenses = ["Apache-2.0"]
            publish = true
        }
    }
}