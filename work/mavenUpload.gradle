apply plugin: 'maven-publish'
apply plugin: 'signing'

// load properties
Properties properties = new Properties()
File projectPropertiesFile = project.file("project.properties")
if(projectPropertiesFile.exists()){
    properties.load(projectPropertiesFile.newDataInputStream())
}

// read properties
def projectName = properties.getProperty("project.name")
def projectGroupId = properties.getProperty("project.groupId")
def projectArtifactId = properties.getProperty("project.artifactId")
def projectVersionName = android.defaultConfig.versionName
def projectPackaging = properties.getProperty("project.packaging")
def projectSiteUrl = properties.getProperty("project.siteUrl")
def projectGitUrl = properties.getProperty("project.gitUrl")

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                artifact releaseSourcesJar

                // You can then customize attributes of the publication as shown below.
                groupId = projectGroupId
                artifactId = projectArtifactId
                version = projectVersionName

                pom {
                    name = projectName
                    packaging = projectPackaging
                    url = projectSiteUrl
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = '\\u8D85\\u609F\\u7A7A'
                            name = '\\u8D85\\u609F\\u7A7A'
                            email = '39251474@163.com'
                        }
                    }
                    scm {
                        connection = projectGitUrl
                        developerConnection = projectGitUrl
                        url = projectSiteUrl
                    }
                }
            }
        }

        repositories {
            maven {
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            }
        }
    }

    signing {
        sign publishing.publications.release
    }
}